(*
   Copyright (c) 2024 Nomadic Labs
   Copyright (c) 2024 Paul Laforgue <paul.laforgue@nomadic-labs.com>
   Copyright (c) 2024 Léo Stefanesco <leo.stefanesco@mpi-sws.org>
   Copyright (c) 2025 Gaëtan Lopez <glopez@irif.fr>

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in all
   copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
   SOFTWARE.
*)

From Stdlib.Unicode Require Import Utf8.
From Stdlib.Program Require Import Equality.
From stdpp Require Import finite gmap decidable.
From TestingTheory Require Import ActTau gLts Bisimulation Lts_OBA Subset_Act WeakTransitions Testing_Predicate
    StateTransitionSystems InteractionBetweenLts Convergence Termination FiniteImageLTS ParallelLTSConstruction.

 (** ** Inductive definition [must_sts] of Must based on the STS *)
Inductive must_sts 
  `{Sts (P1 * P2)} (outcome : P2 -> Prop)
  (p : P1) (t : P2) : Prop :=
| m_sts_now : outcome t -> must_sts outcome p t
| m_sts_step
    (nh : ¬ outcome t)
    (nst : ¬ sts_refuses (p, t))
    (l : forall p' t', sts_step (p, t) (p', t') -> must_sts outcome p' t')
  : must_sts outcome p t
.

Global Hint Constructors must_sts:mdb.

(** ** Inductive definition [must] of Must based on the LTS *)

Inductive must `{
    gLtsP : @gLts P A H,
    gLtsT : ! gLtsEq T H, !Testing_Predicate outcome _}

    `{!Prop_of_Inter P T A dual}

    (p : P) (t : T) : Prop :=
| m_now : outcome t -> must p t
| m_step
    (nh : ¬ outcome t)
    (ex : ∃ t', (p, t) ⟶ t')
    (pt : forall p', p ⟶ p' -> must p' t)
    (et : forall t', t ⟶ t' -> must p t')
    (com : forall p' t' μ1 μ2, dual μ1 μ2
      -> p ⟶[μ1] p' 
        -> t ⟶[μ2] t'  
          -> must p' t')
  : must p t
.

Notation "p 'must_pass' t" := (must p t) (at level 70).

Global Hint Constructors must:mdb.

(** ** Equivalence of the two inductive definitions [must] and [must_sts] *)

Lemma must_sts_iff_must `{
    gLtsP : @gLts P A H,
    gLtsT : ! gLtsEq T H, !Testing_Predicate outcome _}

    `{!Prop_of_Inter P T A dual}

  (p : P) (t : T) :
  must_sts outcome p t <-> p must_pass t.
Proof.
  split.
  - intro hm. induction hm; eauto with mdb.
    apply m_step; eauto with mdb.
    + eapply sts_refuses_spec1 in nst as ((p', t') & hl).
      exists (p', t'). now simpl in hl.
    + simpl in *; eauto with mdb.
    + simpl in *; eauto with mdb.
    + intros p' t' μ1 μ2 duo hl1 hl2.
      apply H0, (ParSync μ1 μ2); eauto.
  - intro hm. dependent induction hm; eauto with mdb.
    eapply m_sts_step; eauto with mdb.
    + eapply sts_refuses_spec2.
      destruct (decide (sts_refuses (p, t))).
      ++ exfalso.
         destruct ex as ((p', t'), hl).
         eapply sts_refuses_spec2 in s; eauto.
         exists (p', t'). now simpl.
      ++ now eapply sts_refuses_spec1 in n.
    + intros p' t' hl.
      inversion hl; subst; eauto with mdb.
Qed.

(** ** Definition of the contextual preorder based on [must] *)

Definition ctx_pre `{
  gLtsP : @gLts P A H, 
  gLtsQ : !gLts Q H, 
  gLtsT : ! gLtsEq T H, !Testing_Predicate outcome _}

  `{!Prop_of_Inter P T A dual}
  `{!Prop_of_Inter Q T A dual}

  (p : P) (q : Q) 
  := forall (t : T), p must_pass t -> q must_pass t.

Global Hint Unfold ctx_pre: mdb.

Notation "p ⊑ₘᵤₛₜᵢ q" := (ctx_pre p q) (at level 70).
Notation "p ⋢ₘᵤₛₜᵢ q" := (¬ ctx_pre p q) (at level 70).

(** ** Properties on [must] *)

Lemma must_eq_client `{
  gLtsP : !@gLts P A H, 
  gLtsT : @gLtsEq T A H, !Testing_Predicate outcome _}
  {_ : Prop_of_Inter P T A dual} :

  forall (p : P) (t t' : T), t ⋍ t' -> p must_pass t -> p must_pass t'.
Proof.
  intros p t t' heq hm.
  revert t' heq.
  dependent induction hm; intros.
  - apply m_now. eapply outcome_preserved_by_eq; eauto.
  - apply m_step; eauto with mdb.
    + intro rh. eapply nh. eapply outcome_preserved_by_eq; eauto with mdb.
      now symmetry.
    + destruct ex as (t'' & l).
      inversion l; subst.
      ++ exists (a2 ▷ t'). eapply ParLeft; eauto.
      ++ symmetry in heq.
         assert (t' ⟶⋍ b2) as (t'' & l3 & l4).
         { eapply eq_spec; eauto. }
         exists (p ▷ t''). eapply ParRight; eauto.
      ++ symmetry in heq.
         assert (t' ⟶⋍[μ2] b2) as (t'' & l3 & l4).
         { eapply eq_spec; eauto. }
         exists (a2 ▷ t''). eapply ParSync; eauto.
    + intros t'' l.
      assert (t ⟶⋍ t'') as (t''' & l3 & l4).
      { eapply eq_spec; eauto. }
      eauto.
    + intros p' r' μ1 μ2 inter l__r l__p.
      assert (t ⟶⋍[μ2] r') as (e' & l__e' & eq').
      { eapply eq_spec; eauto. } eauto.
Qed.

Lemma must_eq_server `{
  gLtsP : !@gLtsEq P A H, 
  gLtsT : ! gLtsEq T H, !Testing_Predicate outcome _} 

  {_ : Prop_of_Inter P T A dual} :

  forall (p q : P) (t : T), p ⋍ q -> p must_pass t -> q must_pass t.
Proof.
  intros p q t heq hm.
  revert q heq.
  dependent induction hm; intros.
  - now apply m_now.
  - apply m_step; eauto with mdb.
    + destruct ex as (t' & l).
      inversion l; subst; eauto with mdb.
      ++ symmetry in heq.
         assert (q ⟶⋍ a2) as (q' & l3 & l4).
         { eapply eq_spec; eauto. }
         exists (q' ▷ t). eapply ParLeft; eauto.
      ++ exists (q ▷ b2). eapply ParRight; eauto.
      ++ symmetry in heq.
         assert (q ⟶⋍[μ1] a2) as (q' & l3 & l4).
         { eapply eq_spec; eauto. }
         exists (q' ▷ b2). eapply ParSync; eauto.
    + intros q' l.
      assert (p ⟶⋍ q') as (p' & l3 & l4).
      { eapply eq_spec; eauto. } eauto.
    + intros q' e' μ1 μ2 inter l__e l__q.
      assert (p ⟶⋍[μ1] q') as (p' & l3 & l4).
      { eapply eq_spec; eauto. } eauto.
Qed.

Lemma must_preserved_by_lts_tau_srv `{
  gLtsP : @gLts P A H, 
  gLtsT : ! gLtsEq T H, !Testing_Predicate outcome _}

  {_ : Prop_of_Inter P T A dual}

  (p1 p2 : P) (t : T) : 
  p1 must_pass t -> p1 ⟶ p2 -> p2 must_pass t.
Proof. by inversion 1; eauto with mdb. Qed.

Lemma must_preserved_by_weak_nil_srv `{
  gLtsP : @gLts P A H, 
  gLtsT : ! gLtsEq T H, !Testing_Predicate outcome _}

  {_ : Prop_of_Inter P T A dual}

  (p q : P) (t : T) : 
  p must_pass t -> p ⟹ q 
    -> q must_pass t.
Proof.
  intros hm w.
  dependent induction w; eauto with mdb.
  eapply IHw; eauto.
  eapply must_preserved_by_lts_tau_srv; eauto.
Qed.

Lemma must_preserved_by_lts_tau_clt `{
  gLtsP : @gLts P A H, 
  gLtsT : ! gLtsEq T H, !Testing_Predicate outcome _}

  {_ : Prop_of_Inter P T A dual}

  (p : P) (e1 e2 : T) : 
  p must_pass e1 -> ¬ outcome e1 -> e1 ⟶ e2 -> p must_pass e2.
Proof. by inversion 1; eauto with mdb. Qed.

Lemma must_preserved_by_synch_if_notoutcome `{
  gLtsP : @gLts P A H, 
  gLtsT : ! gLtsEq T H, !Testing_Predicate outcome _}

  {_ : Prop_of_Inter P T A dual}

  (p p' : P) (t t' : T) μ μ':
  p must_pass t -> ¬ outcome t -> dual μ μ' -> p ⟶[μ] p' -> t ⟶[μ'] t' 
    -> p' must_pass t'.
Proof.
  intros hm u inter l__p l__t.
  inversion hm; subst.
  - contradiction.
  - eapply com; eauto with mdb.
Qed.

Lemma must_preserved_by_lts_tau_clt_rev `{
  gLtsP : @gLts P A H, 
  gLtsT : ! gLtsEq T H, !Testing_Predicate outcome _}

  {_ : Prop_of_Inter P T A dual}

  (p : P) (t1 t2 : T) : 
  p must_pass t2 -> t1 ⟶ t2 -> ¬ outcome t2 -> (forall μ, t1 ↛[μ]) -> (forall t', t1 ⟶ t' -> t' ⋍ t2)
    -> p must_pass t1.
Proof.
  intros must_hyp hyp_tr not_happy not_ext_action tau_determinacy.
  revert t1 hyp_tr not_happy not_ext_action tau_determinacy.
  dependent induction must_hyp.
  - intros. contradiction.
  - intros. destruct (decide (outcome t1)) as [happy' | not_happy'].
    + now eapply m_now.
    + eapply m_step; eauto.
      ++ exists (p ▷ t). eapply ParRight; eauto.
      ++ intros. assert (t ⋍ t'). { symmetry; eauto. }
         eapply must_eq_client; eauto. eauto.
         eapply m_step; eauto.
      ++ intros p' t' μ1 μ2 inter tr_server tr_client. 
         assert (t1 ↛[μ2]); eauto.
         assert (¬ t1 ↛[μ2]). eapply lts_refuses_spec2; eauto. contradiction.
Qed.

Lemma must_preserved_by_lts_tau_clt_rev_rev `{
  gLtsP : @gLts P A H, 
  gLtsT : ! gLtsEq T H, !Testing_Predicate outcome _}

  {_ : Prop_of_Inter P T A dual}

  (p : P) (t1 t2 : T) : 
  p must_pass t2 -> t1 ⟶ t2 -> (forall μ, t1 ↛[μ]) -> (forall t', t1 ⟶ t' -> outcome t') -> p ⤓
    -> p must_pass t1.
Proof.
  intros must_hyp hyp_tr not_ext_action happy_determinacy conv.
  revert t1 t2 must_hyp hyp_tr not_ext_action happy_determinacy.
  dependent induction conv.
  - intros. destruct (decide (outcome t1)) as [happy | not_happy].
    + now eapply m_now.
    + eapply m_step; eauto.
      ++ exists (p ▷ t2). eapply ParRight; eauto.
      ++ intros. assert (must p t2).
      { eapply m_now; eauto. }
      assert (must p' t2).
      { eapply must_preserved_by_lts_tau_srv; eauto. }
      assert (p' ⤓); eauto.
      ++ intros. assert (outcome t'); eauto. now eapply m_now.
      ++ intros. assert (t1 ↛[μ2]); eauto.
         assert (¬ t1 ↛[μ2]). eapply lts_refuses_spec2; eauto. contradiction.
Qed.

Lemma must_terminate_unoutcome `{
  gLtsP : @gLts P A H,
  gLtsT : ! gLtsEq T H, !Testing_Predicate outcome _}

  {_ : Prop_of_Inter P T A dual}

  (p : P) (t : T) : p must_pass t -> ¬ outcome t -> p ⤓.
Proof.
  intros hm. dependent induction hm.
  + contradiction.
  + eauto with mdb.
Qed.

Lemma must_terminate_unoutcome' `{
  gLtsP : @gLts P A H,
  gLtsT : ! gLtsEq T H, !Testing_Predicate outcome _}

  {_ : Prop_of_Inter P T A dual}

  (p : P) (t : T) : p must_pass t -> outcome t \/ p ⤓.
Proof. 
  intros hm. destruct (decide (outcome t)) as [happy | not_happy].
  + now left. 
  + right. eapply must_terminate_unoutcome; eauto.
Qed.

Lemma must_preserved_by_lts_wk_clt `{
  gLtsP : @gLts P A H, 
  gLtsT : ! gLtsEq T H, !Testing_Predicate outcome _}

  {_ : Prop_of_Inter P T A dual}

  (p : P) (t1 t2 : T) : 
  p must_pass t1 -> ¬ outcome t1 -> (∀ t', t1 ⟹ t' -> t' ≠ t2 -> ¬ outcome t') -> t1 ⟹ t2 -> p must_pass t2.
Proof.
  intros Hyp not_happy Hyp_not_happy wk_tr.
  remember t2.
  dependent induction wk_tr. 
  + subst. eauto.
  + subst. assert (∀ t' : T, q ⟹ t' → t' ≠ t2 → ¬ outcome t') as Hyp_final.
    {intros. eapply Hyp_not_happy. econstructor; eauto. eauto. }
    assert (must p q).
    {eapply must_preserved_by_lts_tau_clt; eauto. }
    destruct (decide (q = t2)) as [ eq | not_eq].
    ++ subst. eauto.
    ++ eapply IHwk_tr; eauto. eapply Hyp_not_happy; eauto with mdb.
Qed.

Lemma must_preserved_by_wt_synch_if_notoutcome `{
  gLtsP : @gLts P A H, 
  gLtsT : ! gLtsEq T H, !Testing_Predicate outcome _}

  {_ : Prop_of_Inter P T A dual}

  (p p' : P) (t t' : T) (μ : A) (μ' : A):
  p must_pass t 
    -> ¬ outcome t 
      -> dual μ μ'
        -> p ⟹{μ} p' 
          -> t ⟶[μ'] t' 
            -> p' must_pass t'.
Proof.
  intros hm u duo hwp hwr.
  dependent induction hwp.
  - eapply IHhwp; eauto. eapply must_preserved_by_lts_tau_srv; eauto.
  - eapply must_preserved_by_weak_nil_srv; eauto.
    inversion hm. contradiction. eapply com.
    eassumption. eassumption. eassumption.
Qed.

Lemma ctx_pre_not `{
  gLtsP : @gLts P A H, 
  gLtsQ : !gLts Q H, 
  gLtsT : !gLtsEq T H, !Testing_Predicate outcome _}
  {_ : Prop_of_Inter P T A dual} 
  {_ : Prop_of_Inter Q T A dual}
  (p : P) (q : Q) (t : T) :
  p ⊑ₘᵤₛₜᵢ q -> ¬ q must_pass t -> ¬ p must_pass t.
Proof.
  intros hpre not_must.
  intro Hyp. eapply hpre in Hyp.
  contradiction.
Qed.
